# SPDX-License-Identifier: MIT
# Original by: Marco Dallagiacoma @ 2023 in https://github.com/dallagi/excontainers
# Modified by: Jarl AndrÃ© HÃ¼benthal @ 2023
defmodule Testcontainers.Container.MySqlContainerTest do
  use ExUnit.Case, async: true

  import Testcontainers.ExUnit

  alias Testcontainers.MySqlContainer

  @moduletag timeout: 300_000

  describe "with default configuration" do
    @mysql_container MySqlContainer.new()

    container(:mysql, @mysql_container)

    test "provides a ready-to-use mysql container", %{mysql: mysql} do
      params =
        MySqlContainer.connection_parameters(mysql)
        |> Keyword.merge(queue_target: 30_000, queue_interval: 30_000)

      {:ok, pid} = MyXQL.start_link(params)

      assert %{num_rows: 1} = MyXQL.query!(pid, "SELECT 1", [])
    end
  end

  describe "with custom configuration" do
    import MySqlContainer

    @custom_mysql_container new()
                            |> with_image("mysql/mysql-server:8.0.32-1.2.11-server")
                            |> with_user("custom-user")
                            |> with_password("custom-password")
                            |> with_database("custom-database")

    container(:mysql, @custom_mysql_container)

    test "provides a mysql container compliant with specified configuration", %{mysql: mysql} do
      params =
        MySqlContainer.connection_parameters(mysql)
        |> Keyword.merge(queue_target: 30_000, queue_interval: 30_000)

      {:ok, pid} = MyXQL.start_link(params)

      query_result = MyXQL.query!(pid, "SELECT version()", [])

      version_info = query_result.rows |> Enum.at(0) |> Enum.at(0)
      assert version_info =~ "8.0.32"
    end
  end
end
